
repositories { 
  mavenCentral()
}

configurations.create('osgibundles')
configurations.create('osgimain')
configurations.osgibundles.transitive = false;
configurations.osgimain.transitive = false;

dependencies {
  def openmucversion = '0.15.2'

  osgimain group: 'org.apache.felix', name: 'org.apache.felix.main', version: '5.0.1'

  osgibundles files('../../build/libs/openmuc-core-api-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-core-spi-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-core-datamanager-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-datalogger-ascii-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-datalogger-slotsdb-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-driver-aggregator-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-driver-rest-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-spi-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-base-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-channelconfigurator-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-channelaccesstool-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-userconfigurator-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-dataexporter-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-dataplotter-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-webui-mediaviewer-' + openmucversion + '.jar')
  osgibundles files('../../build/libs/openmuc-server-restws-' + openmucversion + '.jar')

  osgibundles files('../projects/simpledemoapp/build/libs/openmuc-app-simpledemo-' + openmucversion + '.jar')
  osgibundles files('../projects/dummydriver/build/libs/openmuc-driver-dummy-' + openmucversion + '.jar')
  
  //--------------------------------------------------------------------------------------------------
  // The Apache Felix Gogo standard shell for OSGi (http://felix.apache.org/site/apache-felix-gogo.html)
  //--------------------------------------------------------------------------------------------------
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.gogo.runtime', version: '0.16.2'
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.gogo.command', version: '0.14.0'
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.gogo.shell', version: '0.10.0'

  //--------------------------------------------------------------------------------------------------
  // Adds a telnet server so that the Felix Gogo Shell can be accessed
  // using telnet clients. By default this server only listens on
  // localhost port 6666. Therefor you can on only access it from the
  // same host on which felix is running.
  //--------------------------------------------------------------------------------------------------
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.shell.remote', version: '1.1.2'

  //--------------------------------------------------------------------------------------------------
  // message logging libraries, SLF4J is a light-weight logging API,
  // Logback is a message logger implementation that implements SLF4J
  // natively
  //--------------------------------------------------------------------------------------------------
  osgibundles group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
  osgibundles group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
  osgibundles group: 'ch.qos.logback', name: 'logback-core', version: '1.1.3'

  //--------------------------------------------------------------------------------------------------
  // Apache Felix Service Component Runtime that implements the OSGi Declarative Services Specification
  // the OpenMUC core bundles use declarative services and thus depend on them
  //--------------------------------------------------------------------------------------------------
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.scr', version: '2.0.0'

  //--------------------------------------------------------------------------------------------------
  // An implementation of the OSGi HTTP Service Specification, needed by the WebUI bundles
  //--------------------------------------------------------------------------------------------------
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.http.servlet-api', version: '1.1.2'
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.http.api', version: '3.0.0'
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.http.jetty', version: '3.0.2'

  //--------------------------------------------------------------------------------------------------
  // Implementations of the OSGi Event Admin, Configuration Admin and MetaType services, needed by jetty
  //--------------------------------------------------------------------------------------------------
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.eventadmin', version: '1.4.4'
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.configadmin', version: '1.8.8'
  osgibundles group: 'org.apache.felix', name: 'org.apache.felix.metatype', version: '1.1.0'

}

task syncBundles(type: Sync) {
  into rootDir.getPath() + "/bundle"
  from configurations.osgibundles;
}

task copyFelixMain(type: Copy) {
  from configurations.osgimain;
  into rootDir.getPath() + "/felix"
  rename (/org.apache.felix.main-.*jar/, 'felix.jar')
}
